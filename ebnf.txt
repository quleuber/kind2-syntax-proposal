<program> ::= <decl> <s> (<white> <decl> "\n"*)* "\n"*

<s> ::= (" " | "\n")*
<white> ::= (" " | "\n")+
<br> ::= ("\n")*
<num> ::= [0-9]+
<digit> ::= [0-9]
<letter> ::= [A-z]
<name_char_1> ::= <letter> | "_"
<name_char_2> ::= <name_char_1> | <digit>
<name> ::= <name_char_1> <name_char_2>*
<id> ::= <name> ("." <name>)*
<var> ::= <id>

<typed_param> ::=
    "(" <s> <name> <s> ":" <s> <enc_expr> <s> ")"
  | "<" <s> <name> <s> ":" <s> <enc_expr> <s> ">"

<forall_param> ::=
    <typed_param>
  | <expr>
  | "<" <s> <enc_expr> <s> ">"

<param> ::=
    <name>
  | <typed_param>

<decl> ::= <id> (<s> <typed_param>)* <s> ":" <s> <enc_expr> <s> <def>? <s> ";"?
<def> ::=
    "=" <s> <expr>
  | "{" ( <rule> )* <s> "}"
<rule> ::= <s> <enc_app> <s> "=" <s> <expr> <s> ";"?

<expr> ::= 
   "(" <expr> ")"
  | <num>
  | <var>
  |	<let> <expr>
  | <app>
  | <forall>
  | <lambda>

<enc_expr> ::= 
    "(" <expr> ")"
  | <num>
  | <var>
  |	<let> <expr>
  | <enc_app>
  | <forall>
  | <lambda>

<forall> ::= "@" (<s> <forall_param> <s> "->" <s>)+ <expr>
<lambda> ::= "@" (<s> <param> <s> "=>" <s>)+ <expr>

<naked_app> ::= <expr> (<white> <expr>)*
<app> ::= "(" <naked_app> ")"
<enc_app> ::= <app> | <naked_app>

<let> ::= <s> "let" <s> <enc_app> <s> "=" <s> <expr> "in"? <s>





<bop> ::= "&&" | "||"
<bool> ::= "true" | "false"
<eq_op> ::= "==" | "!="
<cmp_op> ::= ">" | ">=" | "<" | "<="
